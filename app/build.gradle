apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

// https://kotlinlang.org/docs/reference/kapt.html
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    defaultConfig {
        applicationId "com.hossainkhan.android.constraintlayout"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 2
        versionName "1.1-constraint-layout-demo"
        versionNameSuffix "-${gitSha()}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibilityVersion
        targetCompatibility rootProject.ext.javaTargetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Android Support libs and Google Android libs
    implementation "com.android.support:support-v13:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"

    // ViewModel and LiveData
    // https://developer.android.com/topic/libraries/architecture/adding-components
    implementation "android.arch.lifecycle:extensions:$rootProject.archComponentVersion"

    // https://developer.chrome.com/multidevice/android/customtabs
    implementation "com.android.support:customtabs:$rootProject.supportLibraryVersion"

    // ========================================================
    // 3rd party libraries
    // ========================================================
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryLibraryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryLibraryVersion"

    implementation "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"

    // Crashlytics
    // https://firebase.google.com/docs/crashlytics/get-started
    implementation "com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion"

    // Firebase Core
    // https://firebase.google.com/docs/android/setup
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseVersion"

    // Dagger
    // https://google.github.io/dagger/
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    // Include if you use the support libraries (eg. Support Activity, Fragment)
    //implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    // A highly customizable, powerful and easy-to-use alerting library for Android.
    // https://github.com/aritraroy/Flashbar
    implementation "com.andrognito.flashbar:flashbar:$rootProject.flashBarVersion"

    // ----------------------------------------------------------------
    // Android Unit and Instrumentation test
    // ----------------------------------------------------------------
    // Required -- JUnit 4 framework
    testImplementation "junit:junit:$rootProject.junitVersion"
    // Mocking framework for unit tests written in Java
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "android.arch.core:core-testing:$rootProject.archComponentVersion"
}

// ADD THIS AT THE BOTTOM
// https://firebase.google.com/docs/android/setup
apply plugin: 'com.google.gms.google-services'